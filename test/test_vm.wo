import woo.std;
import woo.vm;

import test_tool;

namespace test_vm
{
    func load_and_create_vmm(src:string)
    {
        let vmm = std::vm::create();
        vmm->loadsrc("test_vm/test_tmp_script.wo", src);
        return vmm;
    }

    func main()
    {
       let vmm = load_and_create_vmm(
            @"
            import woo.std;
            func main()
            {
                std::println("Hey! This inform come from another vm!~");
            }
            main();
            "@
        );

        test_equal(vmm->haserr(), false);

        vmm->run();
        vmm->close();

        // Test memmory leak
        let mut i = 0;
        while (i < 1000)
        {
            i += 1;
            let vmm = load_and_create_vmm(
                @"
                import woo.std;
                func test_template< T >(n:T)
                {
                    return n;
                }
                func main()
                {
                    let a = [1,2,3,4,5];
                    a->get(0) = 233;

                    if (a[0] != 233)
                        std::panic("Unexcepted reason fail.");

                    test_template(233);
                    test_template("My world!");
                }
                main();
                "@
            );
            test_equal(vmm->haserr(), false);
            if (vmm->haserr())
            {
                std::println(vmm->errmsg());
            }
            vmm->run();

            vmm->close();
        }
    }
}

test_function("test_vm.main", test_vm::main);