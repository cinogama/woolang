import woo::std;
import test_tool;

import pkg::thread;

namespace test_thread
{
    let mut summ = 0;

    let created_threads = []mut: vec<thread>;
    let thread_count = 4;

    let test_vec = []mut: vec<typeof(nil)>;

    func main()
    {
        let mut i = 0;
        while (i<thread_count)
        {
            created_threads->add(
                thread::create(
                    func()
                    {
                        let mut c = 0;
                        while (c < 100000)
                        {
                            test_vec->add(nil);
                            c += 1;
                        }
                        std::println("Thread:", i, "complete");
                    }    
            ));
            i+=1;
        }

        std::println("OK! Waiting to end..");

        i = 0;
        while (i<thread_count)
        {
            do created_threads[i]->wait();
            i+=1;
        }
        std::println("Result:", test_vec->len);
        test_equal(test_vec->len, 100000 * thread_count);

        // Thread should be abort-able..
        let mut this_should_be_false = false;
        let abort_flag = mutable::create(false);
        let __pack = [mut this_should_be_false];
        let th = thread::create(
            \=func(mut i: int)
            {
                __pack[0] = true;
                for (;;)
                {
                    i += 1;
                    do i;

                    if (abort_flag->get)
                        break;
                }
            }(summ););

        abort_flag->set(true);
        test_assure(th->wait == true);

        test_assure(this_should_be_false == false && summ == 0 && __pack[0] == true);

        test_assure(thread::create(\=std::halt("Halt thread for testing.");)->wait == false);
        test_assure(thread::create(\=do nil;)->wait == true);
    }
}

test_function("test_thread.main", test_thread::main);
