import woo.std;
import test_tool;

namespace test_helloworld
{
    let mut _should_not_be_set_flag = false;
    let mut _should_be_set_flag = false;

    func execute_func()
    {
        std::println("You should see this.");
        return _should_be_set_flag = true;
    }
    func non_execute_func()
    {
        std::println("You should not see this.");
        return _should_not_be_set_flag = true;
    }

    func test_no_return_recursive(val: bool)
    {
        if (val)
            test_no_return_recursive(false);
    }

    func test_lambda()
    {
        let a = [1,2,3];

        let b = a->bind(\x:int = [x, x * 2];)
                ->> \x:int = x: string;
                =>> \x:string = [x + "hello", x + "world"];
                ;

        b->map(\x:string = std::println(x););

        test_assure(b[0] == "1hello");
        test_assure(b[1] == "1world");
        test_assure(b[2] == "2hello");
        test_assure(b[3] == "2world");
        test_assure(b[4] == "2hello");
        test_assure(b[5] == "2world");
        test_assure(b[6] == "4hello");
        test_assure(b[7] == "4world");
        test_assure(b[8] == "3hello");
        test_assure(b[9] == "3world");
        test_assure(b[10] == "6hello");
        test_assure(b[11] == "6world");

        test_assure("Helloworld" == "Helloworld"->chars->str);

        "Helloworld"->std::println;

        "Hello"
            ->\m:string = m + "world";
            ->\m:string = std::println(m);;

        func invoker(f: (int)=>int)
        {
            return f(2);
        }

        test_assure(invoker(\n: int=a[n];) == 3);
    }

    func main()
    {
        using std;

        func foo(_: anything)
        {
            return 0;
        }
        let m = 0, msg = "Helloworld";
        let rmsg = m->foo->\n: int = m;->\n:int = m;->\n:int = m;->\n:int = msg;;
        test_assure(rmsg == msg);
        test_assure(rmsg == "Helloworld");

        const let n = "Helloworld~";
        println(n);

        let fhello = func(){println("Helloworld~~")};
        fhello();

        let fhello2 = func(){print("~~"); println("Helloworld~~")};
        fhello2();

        execute_func() || non_execute_func();
        test_assure(_should_be_set_flag && !_should_not_be_set_flag);
        _should_be_set_flag = _should_not_be_set_flag = false;

        !execute_func() && non_execute_func();
        test_assure(_should_be_set_flag && !_should_not_be_set_flag);
        _should_be_set_flag = _should_not_be_set_flag = false;

        (execute_func() || non_execute_func()) && (!execute_func() && non_execute_func());
        test_assure(_should_be_set_flag && !_should_not_be_set_flag);
        _should_be_set_flag = _should_not_be_set_flag = false;

        test_no_return_recursive(true) as void;
        test_no_return_recursive as (bool)=>void;

        \=func(){}; as ()=>()=>void;
    }
}

test_function("test_helloworld.main", test_helloworld::main);
